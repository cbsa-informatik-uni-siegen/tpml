import de.unisiegen.tpml.core.expressions.*;
import de.unisiegen.tpml.core.types.*;


/* Helper class */
action code {:
  private static class IdTypePair {
    public IdTypePair(String id, MonoType tau) { this.id = id; this.tau = tau; }
    String id;
    MonoType tau;
  }
:}


/* Binary operators */
terminal			PLUS, MINUS, STAR, SLASH, MOD;
terminal			EQUAL, LESS, GREATER, LESSEQUAL, GREATEREQUAL;

/* Tuple operators */
terminal			FST, SND;

/* List operators */
terminal			CONS, IS_EMPTY, HD, TL;
terminal			COLONCOLON;

/* Logical operators */
terminal			AMPERAMPER, BARBAR;
terminal			NOT;

/* Syntactic tokens */
terminal			ARROW, COLON, COMMA, DOT;
terminal			SEMI, BELOW_SEMI;
terminal			LPAREN, RPAREN;
terminal			LBRACKET, RBRACKET;
terminal			PARENPAREN;
terminal			BRACKETBRACKET;
terminal			COLONEQUAL, EXCLAMATION;

/* Keywords */
terminal			LAMBDA, LET, REC, REF, IN, IF, THEN, ELSE, WHILE, DO;
terminal			TRUE, FALSE;

/* Types */
terminal			BOOL, INT, UNIT, LIST;

/* Special tokens */
terminal Integer	NUMBER;
terminal Integer	TYPEVARIABLE;
terminal String		IDENTIFIER;
terminal Integer[]	PROJECTION;
terminal			COMMENT;


/* Non-Terminals */
non terminal Expression		seq_expr;
non terminal Expression		expr;
non terminal Expression		simple_expr;
non terminal Expression[]	simple_expr_list;
non terminal Expression		constant;
non terminal Expression		operator;
non terminal Expression[]	expr_semi_list;		/* list of expressions separated by semicolon */
non terminal Expression[]	expr_comma_list;	/* list of expressions separated by comma */
non terminal IdTypePair[]	id_type_list;		/* list of identifiers and optionally types */
non terminal IdTypePair		id_type_pair;		/* (id:type) or id */
non terminal String[]		id_comma_list;		/* list of identifiers separated by comma */
non terminal MonoType		type;
non terminal MonoType		simple_type;
non terminal MonoType[]		type_star_list;		/* list of types separated by star */


/* Precedences, based on http://caml.inria.fr/pub/docs/manual-ocaml/manual015.html */
precedence nonassoc	IN;
precedence nonassoc	BELOW_SEMI;
precedence nonassoc	SEMI;				/* below EQUAL ({lbl = ...; lbl = ...}) */
precedence nonassoc LET, REC;			/* above SEMI (...; let ... in ...) */
precedence nonassoc DO;					/* below THEN (while ... do ...) */
precedence nonassoc THEN;				/* below ELSE (if ... then ...) */
precedence nonassoc ELSE;				/* (if ... then ... else ...) */

precedence right	COLONEQUAL;			/* expr (e := e := e) */

precedence right	ARROW;

precedence right	BARBAR;					/* expr (e || e || e) */
precedence right	AMPERAMPER;				/* expr (e && e && e) */

precedence left		EQUAL, LESS, GREATER, LESSEQUAL, GREATEREQUAL;
precedence right	COLONCOLON;				/* expr (e :: e :: e) */
precedence left		PLUS, MINUS;
precedence left		STAR, SLASH, MOD;



/***************
 * Expressions *
 ***************/


seq_expr			::= expr:e
						{: RESULT = e; :}
						%prec BELOW_SEMI
					|	expr:e1 SEMI seq_expr:e2
						{: RESULT = new Sequence(e1, e2); :}
					;
					
					
expr				::=	simple_expr:e
						{: RESULT = e; :}
				
					/* Applications */
					|	simple_expr:e simple_expr_list:l
						{: for (int n = 0; n < l.length; ++n) {
						     e = new Application(e, l[n]);
						   }
						   RESULT = e; :}

					/* Infix Operations */
					|	expr:e1 COLONEQUAL expr:e2
						{: RESULT = new InfixOperation(new Assign(), e1, e2); :}

					|	expr:e1 PLUS expr:e2
						{: RESULT = new InfixOperation(ArithmeticOperator.newPlus(), e1, e2); :}
					|	expr:e1 MINUS expr:e2
						{: RESULT = new InfixOperation(ArithmeticOperator.newMinus(), e1, e2); :}
					|	expr:e1 STAR expr:e2
						{: RESULT = new InfixOperation(ArithmeticOperator.newMult(), e1, e2); :}
					|	expr:e1 SLASH expr:e2
						{: RESULT = new InfixOperation(ArithmeticOperator.newDiv(), e1, e2); :}
					|	expr:e1 MOD expr:e2
						{: RESULT = new InfixOperation(ArithmeticOperator.newMod(), e1, e2); :}

					|	expr:e1 EQUAL expr:e2
						{: RESULT = new InfixOperation(RelationalOperator.newEquals(), e1, e2); :}
					|	expr:e1 LESS expr:e2
						{: RESULT = new InfixOperation(RelationalOperator.newLowerThan(), e1, e2); :}
					|	expr:e1 GREATER expr:e2
						{: RESULT = new InfixOperation(RelationalOperator.newGreaterThan(), e1, e2); :}
					|	expr:e1 LESSEQUAL expr:e2
						{: RESULT = new InfixOperation(RelationalOperator.newLowerEqual(), e1, e2); :}
					|	expr:e1 GREATEREQUAL expr:e2
						{: RESULT = new InfixOperation(RelationalOperator.newGreaterEqual(), e1, e2); :}

					|	expr:e1 COLONCOLON expr:e2
						{: RESULT = new InfixOperation(new BinaryCons(), e1, e2); :}

					/* Logical operators */
					|	expr:e1 AMPERAMPER expr:e2
						{: RESULT = new And(e1, e2); :}
					|	expr:e1 BARBAR expr:e2
						{: RESULT = new Or(e1, e2); :}

					/* Conditions */
					|	IF seq_expr:e0 THEN seq_expr:e1 ELSE seq_expr:e2
						{: RESULT = new Condition(e0, e1, e2); :}
					|	IF seq_expr:e0 THEN seq_expr:e1
						{: RESULT = new Condition1(e0, e1); :}

					/* Lambda Abstraction */
					|	LAMBDA IDENTIFIER:id DOT seq_expr:e
						{: RESULT = new Lambda(id, null, e); :}
					|	LAMBDA IDENTIFIER:id COLON type:tau DOT seq_expr:e
						{: RESULT = new Lambda(id, tau, e); :}
					|	LAMBDA LPAREN id_comma_list:il RPAREN DOT seq_expr:e
						{: RESULT = new MultiLambda(il, null, e); :}
					|	LAMBDA LPAREN id_comma_list:il RPAREN COLON type:tau DOT seq_expr:e
						{: RESULT = new MultiLambda(il, tau, e); :}

					/* Let */
					|	LET IDENTIFIER:id EQUAL expr:e1 IN expr:e2
						{: RESULT = new Let(id, null, e1, e2); :}
					|	LET IDENTIFIER:id COLON type:tau EQUAL expr:e1 IN expr:e2
						{: RESULT = new Let(id, tau, e1, e2); :}
					|	LET IDENTIFIER:id id_type_list:itl EQUAL expr:e1 IN expr:e2
						{: String[] identifiers = new String[itl.length + 1];
						   MonoType[] types = new MonoType[itl.length + 1];
						   for (int n = 0; n < itl.length; ++n) {
						     identifiers[n + 1] = itl[n].id;
						     types[n + 1] = itl[n].tau;
						   }
						   identifiers[0] = id;
						   RESULT = new CurriedLet(identifiers, types, e1, e2); :}
					|	LET IDENTIFIER:id id_type_list:itl COLON type:tau EQUAL expr:e1 IN expr:e2
						{: String[] identifiers = new String[itl.length + 1];
						   MonoType[] types = new MonoType[itl.length + 1];
						   for (int n = 0; n < itl.length; ++n) {
						     identifiers[n + 1] = itl[n].id;
						     types[n + 1] = itl[n].tau;
						   }
						   identifiers[0] = id;
						   types[0] = tau;
						   RESULT = new CurriedLet(identifiers, types, e1, e2); :}
					|	LET LPAREN id_comma_list:il RPAREN EQUAL seq_expr:e1 IN seq_expr:e2
						{: RESULT = new MultiLet(il, null, e1, e2); :}
					|	LET LPAREN id_comma_list:il RPAREN COLON type:tau EQUAL seq_expr:e1 IN seq_expr:e2
						{: RESULT = new MultiLet(il, tau, e1, e2); :}

					/* Recursion */				
					|	REC IDENTIFIER:id DOT seq_expr:e
						{: RESULT = new Recursion(id, null, e); :}
					|	REC IDENTIFIER:id COLON type:tau DOT seq_expr:e
						{: RESULT = new Recursion(id, tau, e); :}
					|	LET REC IDENTIFIER:id EQUAL expr:e1 IN expr:e2
						{: RESULT = new LetRec(id, null, e1, e2); :}
					|	LET REC IDENTIFIER:id COLON type:tau EQUAL seq_expr:e1 IN seq_expr:e2
						{: RESULT = new LetRec(id, tau, e1, e2); :}
					|	LET REC IDENTIFIER:id id_type_list:itl EQUAL expr:e1 IN expr:e2
						{: String[] identifiers = new String[itl.length + 1];
						   MonoType[] types = new MonoType[itl.length + 1];
						   for (int n = 0; n < itl.length; ++n) {
						     identifiers[n + 1] = itl[n].id;
						     types[n + 1] = itl[n].tau;
						   }
						   identifiers[0] = id;
						   RESULT = new CurriedLetRec(identifiers, types, e1, e2); :}
					|	LET REC IDENTIFIER:id id_type_list:itl COLON type:tau EQUAL expr:e1 IN expr:e2
						{: String[] identifiers = new String[itl.length + 1];
						   MonoType[] types = new MonoType[itl.length + 1];
						   for (int n = 0; n < itl.length; ++n) {
						     identifiers[n + 1] = itl[n].id;
						     types[n + 1] = itl[n].tau;
						   }
						   identifiers[0] = id;
						   types[0] = tau;
						   RESULT = new CurriedLetRec(identifiers, types, e1, e2); :}
						   
					/* Loops */
					|	WHILE seq_expr:e1 DO seq_expr:e2
						{: RESULT = new While(e1, e2); :}
					;


simple_expr			::=	constant:c
						{: RESULT = c; :}
					|	IDENTIFIER:id
						{: RESULT = new Identifier(id); :}

					/* Parenthized operators/expressions */
					|	LPAREN operator:o RPAREN
						{: RESULT = o; :}
					|	LPAREN seq_expr:e RPAREN
						{: RESULT = e; :}

					/* Tuples */
					|	LPAREN expr_comma_list:el RPAREN
						{: RESULT = new Tuple(el); :}
						
					/* Lists */
					|	LBRACKET expr_semi_list:el RBRACKET
						{: RESULT = new List(el); :}
					;


/* for applications */
simple_expr_list	::=	simple_expr:e
						{: RESULT = new Expression[] { e }; :}
					|	simple_expr_list:l simple_expr:e
						{: RESULT = new Expression[l.length + 1];
						   System.arraycopy(l, 0, RESULT, 0, l.length);
						   RESULT[l.length] = e; :}
					;


constant			::=	PARENPAREN		{: RESULT = new UnitConstant(); :}
					|	BRACKETBRACKET	{: RESULT = new EmptyList(); :}
					|	TRUE			{: RESULT = new BooleanConstant(true); :}
					|	FALSE			{: RESULT = new BooleanConstant(false); :}
					|	NUMBER:n		{: RESULT = new IntegerConstant(n.intValue()); :}
					
					/* Logical operators */
					|	NOT				{: RESULT = new Not(); :}

					/* Tuple operators */
					|	PROJECTION:proj	{: RESULT = new Projection(proj[0], proj[1]); :}
					|	FST				{: RESULT = new Fst(); :}
					|	SND				{: RESULT = new Snd(); :}
					
					/* List operators */
					|	CONS			{: RESULT = new UnaryCons(); :}
					|	IS_EMPTY		{: RESULT = new IsEmpty(); :}
					|	HD				{: RESULT = new Hd(); :}
					|	TL				{: RESULT = new Tl(); :}
					
					/* Reference operators */
					|	EXCLAMATION		{: RESULT = new Deref(); :}
					|	REF				{: RESULT = new Ref(); :}
					;


operator			::=	PLUS			{: RESULT = ArithmeticOperator.newPlus(); :}
					|	MINUS			{: RESULT = ArithmeticOperator.newMinus(); :}
					|	STAR			{: RESULT = ArithmeticOperator.newMult(); :}
					|	SLASH			{: RESULT = ArithmeticOperator.newDiv(); :}
					|	MOD				{: RESULT = ArithmeticOperator.newMod(); :}
					|	EQUAL			{: RESULT = RelationalOperator.newEquals(); :}
					|	LESS			{: RESULT = RelationalOperator.newLowerThan(); :}
					|	GREATER			{: RESULT = RelationalOperator.newGreaterThan(); :}
					|	LESSEQUAL		{: RESULT = RelationalOperator.newLowerEqual(); :}
					|	GREATEREQUAL	{: RESULT = RelationalOperator.newGreaterEqual(); :}
					
					/* List operators */
					|	COLONCOLON		{: RESULT = new BinaryCons(); :}
					
					/* Reference operators */
					|	COLONEQUAL		{: RESULT = new Assign(); :}
					;


expr_semi_list		::=	expr:e
						{: RESULT = new Expression[] { e }; :}
					|	expr_semi_list:el SEMI expr:e
						{: RESULT = new Expression[el.length + 1];
						   System.arraycopy(el, 0, RESULT, 0, el.length);
						   RESULT[el.length] = e; :}
					;


expr_comma_list		::=	expr_comma_list:el COMMA seq_expr:e
						{: RESULT = new Expression[el.length + 1];
						   System.arraycopy(el, 0, RESULT, 0, el.length);
						   RESULT[el.length] = e; :}
					|	seq_expr:e1 COMMA seq_expr:e2
						{: RESULT = new Expression[] { e1, e2 }; :}
					;
				
				
id_type_list		::= id_type_list:l id_type_pair:itp
						{: RESULT = new IdTypePair[l.length + 1];
						   System.arraycopy(l, 0, RESULT, 0, l.length);
						   RESULT[l.length] = itp; :}
					|	id_type_pair:itp
						{: RESULT = new IdTypePair[] { itp }; :}
					;
					

id_type_pair		::= LPAREN IDENTIFIER:id COLON type:tau RPAREN
						{: RESULT = new IdTypePair(id, tau); :}
					|	IDENTIFIER:id
						{: RESULT = new IdTypePair(id, null); :}
					;
					
					
id_comma_list		::=	id_comma_list:l COMMA IDENTIFIER:id
						{: RESULT = new String[l.length + 1];
						   System.arraycopy(l, 0, RESULT, 0, l.length);
						   RESULT[l.length] = id; :}
					|	IDENTIFIER:id
						{: RESULT = new String[] { id }; :}
					;

					
					
/*********
 * Types *
 *********/


type				::=	simple_type:t			{: RESULT = t; :}
					|	type:tau REF			{: RESULT = new RefType(tau); :}
					|	type:t1 ARROW type:t2	{: RESULT = new ArrowType(t1, t2); :}
					|	type_star_list:tl
						{: RESULT = new TupleType(tl); :}
					|	type:t LIST				{: RESULT = new ListType(t); :}
					;


simple_type			::=	LPAREN type:t RPAREN	{: RESULT = t; :}
					|	BOOL					{: RESULT = BooleanType.BOOL; :}
					|	INT						{: RESULT = IntegerType.INT; :}
					|	UNIT					{: RESULT = UnitType.UNIT; :}
					|	TYPEVARIABLE:tvar		{: RESULT = new TypeVariable(0, tvar); :}
					;


type_star_list		::= type_star_list:tl STAR type:t
						{: RESULT = new MonoType[tl.length + 1];
						   System.arraycopy(tl, 0, RESULT, 0, tl.length);
						   RESULT[tl.length] = t; :}
					|	type:t1 STAR type:t2
						{: RESULT = new MonoType[] { t1, t2 }; :}
					;
					