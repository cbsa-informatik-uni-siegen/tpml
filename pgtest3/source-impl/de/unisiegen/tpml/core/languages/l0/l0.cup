import de.unisiegen.tpml.core.expressions.*;


/* Syntactic tokens */
terminal		DOT;
terminal		LPAREN;
terminal		RPAREN;
terminal		LAMBDA;
terminal String	IDENTIFIER;

/* Special tokens */
terminal		COMMENT;


/* Non-Terminals */
non terminal Expression		expr;
non terminal Expression		simple_expr;
non terminal Expression[]	simple_expr_list;


expr				::=	simple_expr:e
						{: RESULT = e; :}
					|	simple_expr:e simple_expr_list:l
						{: for (int n = 0; n < l.length; ++n) 
						   {
						     e = new Application(e, l[n],
						       e.getParserStartOffset ( ) , 
						       l[n].getParserEndOffset ( ) );
						   }
						   RESULT = e; :}
					|	LAMBDA:l IDENTIFIER:id DOT expr:e
						{: RESULT = new Lambda(new Identifier (id,idleft,idright), null, e, lleft, eright); :}
					;
		
		
simple_expr			::=	IDENTIFIER:id
						{: RESULT = new Identifier (id,idleft,idright); :}
					|	LPAREN expr:e RPAREN
						{: RESULT = e; :}
					;
			
			
/* for applications */
simple_expr_list	::=	simple_expr:e
						{: RESULT = new Expression[] { e }; :}
					|	simple_expr_list:l simple_expr:e
						{: RESULT = new Expression[l.length + 1];
						   System.arraycopy(l, 0, RESULT, 0, l.length);
						   RESULT[l.length] = e; :}
					;
